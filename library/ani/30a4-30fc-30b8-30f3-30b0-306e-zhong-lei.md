# イージングの種類

![](/assets/easing_styles.png)

各イージングの動きを見比べるためのサンプルです。  
キーボードの←→キーでイージングの種類を切り替えることができます。
横軸を時間軸、縦軸をYの値として数値の変化を赤線で表示しています。
    
###サンプルコード

```java
/*
  this is not really a how to example ... 
  instead this sketch shows you the diffent kind of easings in a very convenient way

  KEYS
  arrow left          : change easing (scroll list)
  arrow right         : change easing (scroll list)
*/

import de.looksgood.ani.*;
import de.looksgood.ani.easing.*;

Easing[] easings = { Ani.LINEAR, Ani.QUAD_IN, Ani.QUAD_OUT, Ani.QUAD_IN_OUT, Ani.CUBIC_IN, Ani.CUBIC_IN_OUT, Ani.CUBIC_OUT, Ani.QUART_IN, Ani.QUART_OUT, Ani.QUART_IN_OUT, Ani.QUINT_IN, Ani.QUINT_OUT, Ani.QUINT_IN_OUT, Ani.SINE_IN, Ani.SINE_OUT, Ani.SINE_IN_OUT, Ani.CIRC_IN, Ani.CIRC_OUT, Ani.CIRC_IN_OUT, Ani.EXPO_IN, Ani.EXPO_OUT, Ani.EXPO_IN_OUT, Ani.BACK_IN, Ani.BACK_OUT, Ani.BACK_IN_OUT, Ani.BOUNCE_IN, Ani.BOUNCE_OUT, Ani.BOUNCE_IN_OUT, Ani.ELASTIC_IN, Ani.ELASTIC_OUT, Ani.ELASTIC_IN_OUT};
String[] easingsVariableNames = {"Ani.LINEAR", "Ani.QUAD_IN", "Ani.QUAD_OUT", "Ani.QUAD_IN_OUT", "Ani.CUBIC_IN", "Ani.CUBIC_IN_OUT", "Ani.CUBIC_OUT", "Ani.QUART_IN", "Ani.QUART_OUT", "Ani.QUART_IN_OUT", "Ani.QUINT_IN", "Ani.QUINT_OUT", "Ani.QUINT_IN_OUT", "Ani.SINE_IN", "Ani.SINE_OUT", "Ani.SINE_IN_OUT", "Ani.CIRC_IN", "Ani.CIRC_OUT", "Ani.CIRC_IN_OUT", "Ani.EXPO_IN", "Ani.EXPO_OUT", "Ani.EXPO_IN_OUT", "Ani.BACK_IN", "Ani.BACK_OUT", "Ani.BACK_IN_OUT", "Ani.BOUNCE_IN", "Ani.BOUNCE_OUT", "Ani.BOUNCE_IN_OUT", "Ani.ELASTIC_IN", "Ani.ELASTIC_OUT", "Ani.ELASTIC_IN_OUT"};
String code =""; 

float x = 700;
float y = 450;
float duration = 90;

int index = 26;
Easing currentEasing = easings[index];

int framesCount = 0;

FloatList pointsY;

void setup() {
  size(850,600);
  smooth();
  stroke(0);

  // you have to call always Ani.init() first!
  Ani.init(this);

  //set Ani's timemode as FRAMES
  Ani.setDefaultTimeMode(Ani.FRAMES);

  pointsY = new FloatList();
  pointsY.append(450.0);

  Ani.to(this, duration, "y", 150, easings[index], "onEnd:stopFramesCount");
}

void draw() {
  background(255);

  pushStyle();
  noFill();
  stroke(100);
  strokeWeight(2);

  // Y axis
  line (100, 450, 100, 120);
  line (100, 120, 95, 125);
  line (100, 120, 105, 125);
  text("Y", 97, 115);

  // t axis
  line(100,450,650,450);
  line(650,450,645,455);
  line(650,450,645,445);
  text("t", 645, 465);

  // START
  line(660, 450, 735, 450);
  text("START", 738, 455);

  // END
  line(120, 150, 735, 150);
  text("END", 738, 155);

  popStyle();

  fill(0);
  int diameter = 50;
  ellipse(x,y,diameter,diameter);

  code = easingsVariableNames[index];
  text(code,10,20);

  if(framesCount <= 89){
    pointsY.append(y);
    framesCount++;
  }

  if(pointsY.size() > 2){
    pushStyle();
    noFill();
    stroke(255,0,0);
    strokeWeight(3);
    beginShape();
    for(int i=0; i < pointsY.size(); i++){            
     vertex(i*6.7+100, pointsY.get(i));
    }
    endShape();  
    popStyle();  
  }
}

void keyReleased() {
  if (keyCode == LEFT) index--;
  if (keyCode == RIGHT) index++;
  index = constrain(index,0,easings.length-1);
  y = 450;
  framesCount = 0;
  pointsY.clear();
  pointsY.append(y);
  Ani.to(this, duration, "y", 150, easings[index], "onEnd:stopFramesCount");
}
```



